comentario_bloco = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
comentario_linha = _{ "//" ~ (!("\r" | "\n") ~ ANY)* }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" | comentario_linha | comentario_bloco }

arquivo = { SOI ~ expressao ~ EOI }

expressao = { atribuicao* ~ logica_ou }

atribuicao = { nome ~ "=" ~ expressao }

nome = @{ (LETTER | "_") ~ (LETTER | NUMBER | "_")* }

logica_ou  = { logica_e ~ (ou ~ logica_e)* }
logica_e   = { comparacao ~ (e ~ comparacao)* }
comparacao = { soma ~ ((maior_igual | menor_igual | maior | menor | igual | diferente) ~ soma)* }
soma       = { produto ~ ((adicao | subtracao) ~ produto)* }
produto    = { unario ~ ((multiplicacao | divisao) ~ unario)* }
unario     = { negacao? ~ atomo }
atomo      = { "(" ~ expressao ~ ")" | numero_literal | texto_literal | nome }

ou = { "||" }

e = { "&&" }

maior_igual = { ">=" }
menor_igual = { "<=" }
maior       = { ">" }
menor       = { "<" }
igual       = { "==" }
diferente   = { "!=" }

adicao    = { "+" }
subtracao = { "-" }

multiplicacao = { "*" }
divisao       = { "/" }

negacao       = { "!" }

numero_literal = @{ "-"? ~ (ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? | "." ~ ASCII_DIGIT+) }
texto_literal  = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }